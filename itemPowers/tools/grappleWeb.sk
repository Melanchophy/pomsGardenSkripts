on right click:
    if custom model data of player's tool is 67:
        if player is not sneaking:
            if distance between player and player's targeted block is less than 25:
                cooldown(player, "webslinger", 0.3 seconds) is true:
                    if amount of indexes of {te::player::%player's uuid%::grappleWebs::*} is greater than 3:
                        clear {te::player::%player's uuid%::grappleWebs::%first element of {te::player::%player's uuid%::grappleWebs::*}%}
                    
                    if {te::player::%player's uuid%::grappleWebs::*} is set:
                        set {_tb} to last element of {te::player::%player's uuid%::grappleWebs::*} + 1
                    else:
                        set {_tb} to 1
                    set {_tbl} to target block's location

                    set {te::player::%player's uuid%::grappleWebs::%{_tb}%} to {_tb}

                    apply slowness 2 to player for 0.8 seconds
                    play sound "entity.arrow.hit" in voice category with pitch 1.1 for player at {_tbl}
                    play sound "entity.spider.ambient" in voice category with pitch 1.6 at player

                    while {te::player::%player's uuid%::grappleWebs::%{_tb}%} is set:
                        set {_dist} to distance between player and {_tbl}
                        if {_dist} is greater than 60:
                            clear {te::player::%player's uuid%::grappleWebs::*}
                            exit
                        if {_dist} is greater than 5:
                            set {_v} to vector between player and {_tbl}
                            push player {_v} at speed sqrt({_dist})/15/amount of indexes of {te::player::%player's uuid%::grappleWebs::*}
                        else:
                            set {_v} to vector between {_tbl} and player
                            push player {_v} at speed sqrt({_dist})/30/amount of indexes of {te::player::%player's uuid%::grappleWebs::*}
                        particleLine(location 0.4 block above player, {_tbl}, crit, 2)
                        wait 2 ticks
                        apply slow falling without particles to player for 0.2 second replacing existing effect
        else:
            cooldown(player, "webslinger", 0.4 seconds) is true:
                send "&7&oCleared all your webs."
                clear {te::player::%player's uuid%::grappleWebs::*}